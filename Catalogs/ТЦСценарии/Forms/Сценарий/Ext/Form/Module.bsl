
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.КоличествоОдновременноЗапускаемыхВРМАгента < 1 Тогда
		Объект.КоличествоОдновременноЗапускаемыхВРМАгента = 10;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоТаймаутПодготовки(Команда)
	
	Максимум = МаксимумКлиентовНаКомпьютере();
	Объект.ТаймаутПодготовки =
		ТЦОбщий.ОкрВверх(Максимум / 10) * Объект.ТаймаутЗапускаКлиента + 60;
	
КонецПроцедуры // АвтоТаймаутПодготовки()

&НаСервере
// Определить максимальное количество клиентов, запускаемых на одном компьютере
//
// Параметры:
//  Сценарий
//
// Возвращаемое значение:
//  Число - максимальное количество клиентов
//
Функция МаксимумКлиентовНаКомпьютере()
	
	Компьютеры = Объект.Структура.Выгрузить(, "Клиент, Количество");
	Компьютеры.Колонки.Добавить("Компьютер");
	
	Для каждого СтрокаКомпьютера Из Компьютеры Цикл
		СтрокаКомпьютера.Компьютер = СтрокаКомпьютера.Клиент.Компьютер;
	КонецЦикла;
	
	Компьютеры.Свернуть("Компьютер", "Количество");
	Максимум = 0;
	
	Для каждого СтрокаКомпьютера Из Компьютеры Цикл
		Если СтрокаКомпьютера.Количество > Максимум Тогда
			Максимум = СтрокаКомпьютера.Количество;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Максимум;
	
КонецФункции // МаксимумКлиентовНаКомпьютере()

&НаКлиенте
Процедура СтруктураКлиентПриИзменении(Элемент)
	
	Если ТЦСервер.ЭтоВебКлиент(Элементы.Структура.ТекущиеДанные.Клиент) Тогда
		Элементы.СтруктураКоличество.МаксимальноеЗначение = 1;
		Элементы.Структура.ТекущиеДанные.Количество = 1;
	Иначе
		Элементы.СтруктураКоличество.МаксимальноеЗначение = 1000000;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураРольОткрытие(Элемент, СтандартнаяОбработка)
	
	П = Новый Структура;
	П.Вставить("Ключ", Элементы.Структура.ТекущиеДанные.Роль);
	П.Вставить("ТЦСценарий", Объект.Ссылка);
	ОткрытьФорму("Справочник.ТЦРоли.Форма.Роль", П);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // СтруктураРольОткрытие()
