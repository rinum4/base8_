&НаКлиенте
Перем ФормаОбработки;

// Открывает тестовую обработку для настройки параметров для роли
//
// Параметры:
//  Команда
//
&НаКлиенте
Процедура Настроить(Команда)
	
	Попытка
		
		ДатаНачала = ТекущаяДата();
		ИмяФормыОбработки = ТЦКлиент.ПолучитьИмяФормыОбработки(Объект.Обработка);
		П = Новый Структура;
		П.Вставить("ТЦСценарий", Параметры.ТЦСценарий);
		П.Вставить("ТЦРоль", Объект.Ссылка);
		
		ФормаОбработки = ПолучитьФорму(ИмяФормыОбработки, П);
		ФормаОбработки.ТЦЗагрузить(ПолучитьПараметры());
		ФормаОбработки.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьПараметрыРоли", ЭтотОбъект);
		
		ФормаОбработки.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаОбработки.Открыть();
		
	Исключение
		
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
		
	КонецПопытки;
	
КонецПроцедуры // Настроить()

// Сохранить параметры тестовой обработки для роли
//
// Параметры:
//  Результат		 - результат закрытия тестовой обработки (значение Истина означает, что параметры обработки необходимо сохранить).
//
&НаКлиенте
Процедура СохранитьПараметрыРоли(Результат, ДополнительныеПараметры) Экспорт
	
	ДатаНачала = ТекущаяДата();
	
	Если Результат = Истина Тогда
		
		Попытка
			
			СохранитьПараметры(ФормаОбработки.ТЦСохранить());
			
		Исключение
			
			Ошибка = ИнформацияОбОшибке();
			ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
			ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
			
		КонецПопытки;

	КонецЕсли;
	
	ФормаОбработки = Неопределено;
	
КонецПроцедуры

&НаСервере
// Получить параметры обработки
//
// Возвращаемое значение:
//  ХранилищеЗначения
//
Функция ПолучитьПараметры()
	
	Возврат Объект.Ссылка.Параметры;
	
КонецФункции // ПолучитьПараметры()

&НаСервере
// Сохранить параметры обработки
//
// Параметры:
//  ПараметрыОбработки - ХранилищеЗначения
//
Процедура СохранитьПараметры(ПараметрыОбработки)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Параметры = ПараметрыОбработки;
	ТекущийОбъект.Записать();
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
КонецПроцедуры // СохранитьПараметры()

&НаКлиенте
Процедура ОбработкаПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
// Установить доступность элементов упрвления
//
Процедура УстановитьДоступность()
	
	Элементы.Настроить.Доступность = ЗначениеЗаполнено(Объект.Обработка);
	
КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
// Обработчик открытия формы
//
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры // ПриОткрытии()
