&НаКлиенте
Перем кВнешняя;

&НаКлиенте
Перем кВнутреняя;


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИмяВнешняя = "Перечисление.ТЦТипОбработки.Внешняя";
	кВнешняя = ПредопределенноеЗначение(ИмяВнешняя);
	ИмяВнутренняя = "Перечисление.ТЦТипОбработки.Внутренняя";
	кВнутреняя = ПредопределенноеЗначение(ИмяВнутренняя);
	
	УстановитьДоступностьЭлементовУправления();
	
КонецПроцедуры

&НаКлиенте
// Обработчик изменения типа тестовой обработки
//
Процедура ТипПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовУправления();
	
КонецПроцедуры

&НаСервере
// Обработчик создания формы на сервере
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		// Загрузка макета для новой обработки
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");
		
		Если ТекущийОбъект.ЭтоНовый() Тогда
			АдресФайла = ПоместитьВоВременноеХранилище(ТекущийОбъект.ПолучитьМакет("Шаблон"), Новый УникальныйИдентификатор);
			ИмяФайла = "ТестоваяОбработка.epf";
		Иначе
			АдресФайла = ПоместитьВоВременноеХранилище(ТекущийОбъект.Файл.Получить(), Новый УникальныйИдентификатор);
		КонецЕсли;
		
		// Установка списка выбора имени внутренней обработки
		ТестовыеОбработки = ТЦСервер.СписокТестовыхОбработок();
		СписокВыбора = Элементы.ИмяОбработки.СписокВыбора;
		
		Для каждого ТестоваяОбработка Из ТестовыеОбработки Цикл
			СписокВыбора.Добавить(ТестоваяОбработка.Имя, ТестоваяОбработка.Синоним);
		КонецЦикла;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// 
//
// Параметры:
//  
//
// Возвращаемое значение:
//  
//
Функция СоздатьФайловыйДиалог(ТипДиалога)
	
	Диалог = Новый ДиалогВыбораФайла(ТипДиалога);
	Диалог.ПолноеИмяФайла = Объект.ИмяОбработки;
	Диалог.Фильтр = "Тестовая обработка(*.epf)|*.epf";
	Диалог.ПроверятьСуществованиеФайла = Истина;
	
	Возврат Диалог;
	
КонецФункции // СоздатьФайловыйДиалог()

&НаКлиенте
Процедура ИмпортОбработки(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		Если Объект.Тип = ПредопределенноеЗначение("Перечисление.ТЦТипОбработки.Внешняя") Тогда
			Диалог = СоздатьФайловыйДиалог(РежимДиалогаВыбораФайла.Открытие);
			
			Если Диалог.Выбрать() Тогда
				ПоместитьФайл(АдресФайла, Диалог.ПолноеИмяФайла,, Ложь);
				ИмяФайла = ТЦОбщий.ИмяФайла(Диалог.ПолноеИмяФайла);
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭкспортОбработки(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		Если Объект.Тип = ПредопределенноеЗначение("Перечисление.ТЦТипОбработки.Внешняя") Тогда
			Диалог = СоздатьФайловыйДиалог(РежимДиалогаВыбораФайла.Сохранение);
			Диалог.ПолноеИмяФайла = Объект.ИмяОбработки;
			
			Если Диалог.Выбрать() Тогда
				ПолучитьФайл(АдресФайла, Диалог.ПолноеИмяФайла, Ложь);
			КонецЕсли;
		КонецЕсли;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// 
//
// Параметры:
//  
//
Процедура УстановитьДоступностьЭлементовУправления()
	
	ЭтоВнешняя = Объект.Тип = кВнешняя;
	ЭтоВнутреняя = Объект.Тип = кВнутреняя;
	Элементы.Импорт.Доступность = ЭтоВнешняя;
	Элементы.Экспорт.Доступность = ЭтоВнешняя;
	Элементы.ИмяОбработки.Доступность = ЭтоВнутреняя;
	
КонецПроцедуры // УстановитьДоступностьЭлементовУправления()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		Если Не ПустаяСтрока(АдресФайла) Тогда
			ТекущийОбъект.Файл = Новый ХранилищеЗначения(
				ПолучитьИзВременногоХранилища(АдресФайла),
				Новый СжатиеДанных(9));
			ТекущийОбъект.ИмяФайла = ИмяФайла;
			АдресФайла = "";
		КонецЕсли;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры
