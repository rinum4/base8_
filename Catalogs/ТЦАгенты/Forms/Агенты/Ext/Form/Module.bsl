&НаКлиенте
Перем мВыгрузка;

&НаКлиенте
Перем мНачалоВыгрузки;


&НаКлиенте
Процедура ВключитьРежимАгента(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		ТЦКлиент.ВключитьРежимАгента();
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// 
//
// Параметры:
//  
//
Процедура ОжиданиеЗавершенияВыгрузкиАгента()
	
	Готово = Ложь;
	
	Если ТекущаяДата() - мНачалоВыгрузки < 10 Тогда
		ЗапущенныеАгенты = ТЦСервер.КлиентыЗапущены(Элементы.Список.ВыделенныеСтроки);
		
		Если ЗапущенныеАгенты.Количество() = 0 Тогда
			Готово = Истина;
		КонецЕсли;
	Иначе
		ЗапущенныеАгенты = ТЦСервер.КлиентыЗапущены(Элементы.Список.ВыделенныеСтроки);
		
		Если ЗапущенныеАгенты.Количество() > 0 Тогда
			ТЦСервер.УдалитьКлиентов(ЗапущенныеАгенты);
		КонецЕсли;
		
		Готово = Истина;
	КонецЕсли;
	
	Если Готово Тогда
		Элементы.Список.Обновить();
		ОтключитьОбработчикОжидания("ОжиданиеЗавершенияВыгрузкиАгента");
		Элементы.ВыгрузитьАгента.Заголовок = "Выгрузить";
		мВыгрузка = Ложь;
		УстановитьДоступность();
	КонецЕсли;
	
КонецПроцедуры // ОжиданиеЗавершенияВыгрузкиАгента()

&НаКлиенте
Процедура ВыгрузитьАгента(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		ОтключитьОбработчикОжидания("ОжиданиеЗавершенияВыгрузкиАгента");
		ТЦСервер.ЗавершитьРаботуАгента(Элементы.Список.ВыделенныеСтроки);
		мНачалоВыгрузки = ТекущаяДата();
		Элементы.ВыгрузитьАгента.Заголовок = "Выгрузка...";
		мВыгрузка = Истина;
		УстановитьДоступность();
		ПодключитьОбработчикОжидания("ОжиданиеЗавершенияВыгрузкиАгента", 1);
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// 
//
// Параметры:
//  
//
Процедура УстановитьДоступность()
	
	ПустаяСтрока = Не ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока);
	Элементы.ВыгрузитьАгента.Доступность = Не ПустаяСтрока И мВыгрузка <> Истина;
	
КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		УстановитьДоступность();
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры
