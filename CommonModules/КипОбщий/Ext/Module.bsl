///////////////////////////////////////////////////////////////////////////////
// ФАЙЛОВАЯ СИСТЕМА

// Получить символ разделителя каталогов в зависимости от типа ОС
//
// Возвращаемое значение:
//  Строка - Символ разделяющий каталоги и файлы
//
Функция РазделительКаталогов() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	ЭтоWindows = СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86 Или
		СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64;
	
	Возврат ?(ЭтоWindows, "\", "/");
	
КонецФункции // РазделительКаталогов()

// Получить символ разделителя пути в зависимости от типа ОС
//
// Возвращаемое значение:
//  Строка - Символ разделяющий элементы пути
//
Функция РазделительПути() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	ЭтоWindows = СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86 Или
		СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64;
	
	Возврат ?(ЭтоWindows, ";", ":");
	
КонецФункции // РазделительПути()


///////////////////////////////////////////////////////////////////////////////
// ФОРМАТИРОВАНИЕ

// Разделить строку на составляющие по разделителю и поместить
// части строки в массив
//
// Параметры:
//  РазделяемаяСтрока - Строка, которую нужно разделить
//  Разделитель - Строка, символ разделяющий части строки
//  ПустыеСтроки - Булево, Истина - включать пустые строки в результат,
//                 Ложь - не включать пустые строки в результат
//
// Возвращаемое значение:
//  Массив - части строк
//
Функция РазделитьСтроку(ИсходнаяСтрока, Разделитель, ПустыеСтроки = Ложь) Экспорт
	
	Результат = Новый Массив;
	ДлинаСтроки = СтрДлина(ИсходнаяСтрока);
	ТекущаяСтрока = "";
	
	Для Сч = 1 По ДлинаСтроки Цикл
		
		ТекущийСимвол = Сред(ИсходнаяСтрока, Сч, 1);
		
		Если ТекущийСимвол = Разделитель Тогда
			
			ОбработаннаяСтрока = СокрЛП(ТекущаяСтрока);
			
			Если Не ПустаяСтрока(ОбработаннаяСтрока) Или ПустыеСтроки Тогда
				Результат.Добавить(ОбработаннаяСтрока);
			КонецЕсли;
			
			ТекущаяСтрока = "";
			
		Иначе
			ТекущаяСтрока = ТекущаяСтрока + ТекущийСимвол;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбработаннаяСтрока = СокрЛП(ТекущаяСтрока);
	
	Если Не ПустаяСтрока(ОбработаннаяСтрока) Тогда
		Результат.Добавить(ОбработаннаяСтрока);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // РазделитьСтроку()

// Объединить строку из массива
// части строки в массив
//
// Параметры:
//  МассивСтрок - Строка, строки, которые нужно объединить
//  Разделитель - Строка, символ разделяющий части строки
//
// Возвращаемое значение:
//  Строка - объединенная строка
//
Функция ОбъединитьСтроку(МассивСтрок, Разделитель) Экспорт
	
	Результат = "";
	
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		
		Если Не ПустаяСтрока(Результат) Тогда
			Результат = Результат + Разделитель;
		КонецЕсли;
		
		Результат = Результат + СтрокаМассива;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ОбъединитьСтроку()
