////////////////////////////////////////////////////////////////////////////////
// Обработчики ожидания для подсистемы Тест Центр
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
// Обработчик ожидания для опроса справочника ТЦУправлениеТестом (выполняется, если тип приложения не назначен)
//
Процедура ОпросУправленияТестомПоИд() Экспорт
	
	ТипПриложения = ТЦСервер.ПолучитьТипПриложенияПоИдентификатору();
	Если ЗначениеЗаполнено(ТипПриложения) Тогда
		ТЦКлиент.ПереключитьРежимОпроса(Истина);
		ТЦСервер.УстановитьТипКлиентскогоПриложения(ТипПриложения);
		Если ТипПриложения = ПредопределенноеЗначение("Перечисление.ТЦТипКлиентскогоПриложения.АгентТЦ") Тогда
			ТЦКлиент.ВключитьРежимАгента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ОпросУправленияТестомПоИд()

&НаКлиенте
// Обработчик ожидания для опроса справочника ТЦУправлениеТестом (выполняется, если тип приложения назначен)
//
Процедура ОпросУправленияТестомДляРолей() Экспорт
	
	ТипПриложения = ТЦКлиент.ВыполнитьОпросУправленияТестомДляРолей();
	Если Не ЗначениеЗаполнено(ТипПриложения) Тогда
		ТЦКлиент.ПереключитьРежимОпроса(Ложь);
	КонецЕсли;

КонецПроцедуры //ОпросУправленияТестомДляРолей()

&НаКлиенте
// Опрашивает статус обновления информационной базы.
//
Процедура ОпросОбновленияИнформационнойБазы() Экспорт

	Если НЕ ТЦСервер.БСПВыполняетсяОбновлениеИнформационнойБазы() Тогда
		
		ОтключитьОбработчикОжидания("ОпросОбновленияИнформационнойБазы");
		
		Попытка
			ТЦСервер.БСПСоздатьГруппуДоступаТестЦентр();
		Исключение
			ТЦОбщий.ЗаписатьВЖурнал(ИнформацияОбОшибке(), "Тест-центр");
		КонецПопытки;
		
	КонецЕсли;

КонецПроцедуры // ОпросОбновленияИБ()
